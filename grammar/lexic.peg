/* Lexical Grammar */

SourceCharacter
  = .

WhiteSpace "whitespace"
  = [\t\v\f \u00A0\uFEFF]
  / Zs

// use [\w|\W]
IdentifierLetter
  = [a-zA-Z0-9\-_\^\º\ç\.\$\@\€\?\%\+\;]

ElementSeparator
  = EOSNoLineTerminator
  / ","

LineTerminator
  = [\n\r\u2028\u2029]

LineTerminatorSequence "end of line"
  = "\n"
  / "\r\n"
  / "\r"
  / "\u2028" // line separator
  / "\u2029" // paragraph separator

EOF
  = !.

EOL
  = LineTerminatorSequence

EOS
  = _ LineTerminatorSequence
  / _ EOF
  / _ &"]"
  / __ EndToken

EOSNoLineTerminator
  = _ LineTerminatorSequence
  / _ EOF

/* Comments */

Comment "comment"
  = comment:(MultiLineComment
  / SingleLineComment) {
    if (includeComments) {
      registerComment(comment)
    }
  }

MultiLineComment
  = "##" comment:(!"##" c:SourceCharacter { return c; })* "##" {
    return astNode({
      type: 'Block',
      value: comment.join('').trim()
    })
  }

MultiLineCommentNoLineTerminator
  = "##" comment:(!("##" / LineTerminator) c:SourceCharacter { return c; })* "##" {
    return astNode({
      type: 'Block',
      value: comment.join('').trim()
    })
  }

SingleLineComment
  = "#" comment:(!LineTerminator c:SourceCharacter { return c })* {
    return astNode({
      type: 'Line',
      value: comment.join('').trim()
    })
  }

/* Experimental indent-based parsing */

LINE
  = SAMEDENT LINE:(!EOS c:SourceElements { return c; })+ EOS?
    children:( INDENT c:LINE* DEDENT { return c; })?
    {
      var o = {};
      o[LINE] = children;
      return children ? o : LINE.join('');
    }

SAMEDENT
  = i:[ \t]* &{ return i.join('') === indent; }

INDENT
  = i:[ \t]+ &{ return i.length > indent.length; }
    {
      indentStack.push(indent);
      indent = i.join('');
      pos = offset;
    }

DEDENT
  = { indent = indentStack.pop(); }
