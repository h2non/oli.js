/* Statements */

Statement
  = Comment
  / CopyStatement
  / ValueBlockStatement
  / BlockStatement

StatementList
  = Statement
  / PrimitiveValueExpression
  / UnquotedStringListLiteral
  / UnquotedStringLiteral

BlockStatementList
  = head:StatementList tail:(__ ElementSeparator? __ StatementList)* {
    var result = [ head ];
    for (var i = 0, l = tail.length; i < l; i+=1) {
      result.push(tail[i][3]);
    }
    return result;
  }

BlockElement
  = head:BlockStatementList tail:(__ ElementSeparator?
   __ value:BlockStatementList { return value; })* {
      var result = [ head ];
      for (var i = 0, l = tail.length; i < l; i+=1) {
        result.push(tail[i]);
      }
      return result;
    }

CopyStatement
  = id:IdentifierExpression __ CopyToken
      __ source:IdentifierExpression __
    __ (EndToken / EOS) {
      return astNode({
        type:     "CopyStatement",
        id:       id,
        source:   source
      });
    }

ValueBlockStatement
  = id:IdentifierExpression _ StartToken
      _ !LineTerminatorSequence
      _ body:(Statement / body:(PrimitiveValueExpression / UnquotedStringListLiteral) _
      EOS { return body; }) {
      return astNode({
        type:     "ValueBlockStatement",
        id:       id,
        body:     body !== null ?
          (Array.isArray(body) ? body : [ body ]) : []
      });
    }

// todo: pending create specific nested blocks
// with support for list of elements comma separated
BlockStatement
  = id:IdentifierExpression __ StartToken __
    body:BlockStatementList __ EndToken {
      return astNode({
        type:     "BlockStatement",
        id:       id,
        body:     body !== null ? body : []
      });
    }
