/* Statements */

PrimitiveTypeStatement
  = PrimitiveValueExpression
  / UnquotedStringLiteral

MainStatement
  = Comment
  / CopyStatement
  / ValueBlockStatement
  / BlockStatement

Statement
  = MainStatement
  / PrimitiveTypeStatement

StatementList
  = Statement
  / PrimitiveValueExpression
  / UnquotedStringLiteral

ListElementStatement
  = MainStatement
  / PrimitiveValueExpression
  / UnquotedStringListLiteral

BlockStatementList
  = head:StatementList tail:(__ ElementSeparator? __ StatementList)* {
    var result = [ head ];
    for (var i = 0, l = tail.length; i < l; i+=1) {
      result.push(tail[i][3]);
    }
    return result;
  }

ListBlockStatement
  = head:ListElementStatement tail:(__ ElementSeparator? __ ListElementStatement)* {
    var result = [ head ];
    for (var i = 0, l = tail.length; i < l; i+=1) {
      result.push(tail[i][3]);
    }
    return result;
  }

CopyStatement
  = id:IdentifierExpression __ CopyToken
      __ source:IdentifierExpression __
    __ EOS {
      return astNode({
        type:     "CopyStatement",
        id:       id,
        source:   source
      });
    }

ValueBlockStatement
  = id:IdentifierExpression _ StartToken
      _ !LineTerminatorSequence
      _ body:(MainStatement / body:PrimitiveTypeStatement _ EOS { return body; }) {
      return astNode({
        type:     "ValueBlockStatement",
        id:       id,
        body:     body !== null ?
          (Array.isArray(body) ? body : [ body ]) : []
      });
    }

// todo: pending create specific nested blocks
// with support for list of elements comma separated
BlockStatement
  = id:IdentifierExpression __ StartToken __
    body:BlockStatementList __ EndToken {
      return astNode({
        type:     "BlockStatement",
        id:       id,
        body:     body !== null ? body : []
      });
    }
