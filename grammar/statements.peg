/* Statements */

PrimitiveTypeStatement
  = PrimitiveValueExpression
  / UnquotedStringLiteral

MainStatement
  = Comment
  / ValueStatement
  / BlockStatement

Statement
  = MainStatement
  / PrimitiveTypeStatement

StatementList
  = Statement
  / PrimitiveValueExpression
  / UnquotedStringLiteral

ListElementStatement
  = MainStatement
  / PrimitiveValueExpression
  / UnquotedStringListLiteral

BlockAsignmentOperator
  = operator:(
    VariableAssignOperator
  / AsignNotOperator
  / AsignRawOperator
  / AsignUnfoldOperator
  / AsignFoldOperator
  / StartToken) {
    return operator;
  }

ListBlockStatement
  = head:ListElementStatement tail:(__ ElementSeparator? __ ListElementStatement)* {
    var result = [ head ];
    for (var i = 0, l = tail.length; i < l; i+=1) {
      result.push(tail[i][3]);
    }
    return result;
  }

BlockElementStatement
  = head:StatementList tail:(__ ElementSeparator? __ StatementList)* {
    var result = [ head ];
    for (var i = 0, l = tail.length; i < l; i+=1) {
      result.push(tail[i][3]);
    }
    return result;
  }

ValueStatement
  = id:IdentifierExpression _ operator:AsignNotOperator _ EOS {
      return astNode({
        type:    'ExpressionStatement',
        expression: astNode({
          type:     'ValueStatement',
          operator: operator,
          left:     id,
          right:    null
        })
      });
    }
  / id:IdentifierExpression _ operator:BlockAsignmentOperator
      _ !LineTerminatorSequence
      _ body:(MainStatement / body:PrimitiveTypeStatement _ EOS { return body; })
    {
      return astNode({
        type:    'ExpressionStatement',
        expression: astNode({
          type:     'ValueStatement',
          operator: operator,
          left:     id,
          right:    operator === ':!' ? null : body
        })
      });
    }

BlockStatement
  = id:IdentifierExpression __ operator:BlockAsignmentOperator
    __ body:BlockElementStatement __ EndToken
    {
      return astNode({
        type:    'ExpressionStatement',
        expression: astNode({
          type:     'BlockStatement',
          operator: operator,
          left:     id,
          right:    operator === ':!' ? null : {
            type: 'BodyStatement',
            body: body !== null ? body : []
          }
        })
      });
    }
